{
  description = "Consul Deploy";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/5516b991bcc4";
    # terraform module
    terranix = {
      url = "github:terranix/terranix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    # generate imgs
    nixos-generators = {
      url = "github:nix-community/nixos-generators";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { self, nixpkgs, terranix, nixos-generators, ... }@inputs:
    let
      pkgs = import nixpkgs {
        inherit system;
        overlays = [ self.overlays.default ];
      };
      system = "x86_64-linux";
      terraform =
        pkgs.terraform.withPlugins (p: [ p.null p.external p.libvirt ]);
      terraformConfiguration = terranix.lib.terranixConfiguration {
        inherit system pkgs;
        modules = [ ./provision.nix ];
      };
    in {
      # overlay
      overlays.default = import ./overlays;
      # Packages
      packages.${system} = {
        devShell =
          import ./devShell.nix ({ inherit pkgs nixpkgs system; } // inputs);
        qcow = nixos-generators.nixosGenerate {
          inherit pkgs;
          modules = [
            # minimal libvirt qcow
            ./generators/minimal-libvirt.nix
          ];
          format = "qcow";
        };
      };
      devShells.${system}.default = self.packages.${system}.devShell;

      # Apps
      apps.${system} = {
        # nix run ".#apply"
        apply = {
          type = "app";
          program = toString (pkgs.writers.writeBash "apply" ''
            if [[ -e config.tf.json ]]; then rm -f config.tf.json; fi
            ${pkgs.git}/bin/git add -Nf result \
              && cp ${terraformConfiguration} config.tf.json \
              && ${terraform}/bin/terraform init \
              && ${terraform}/bin/terraform apply \
              && ${terraform}/bin/terraform output -json > output.json \
              && ${pkgs.git}/bin/git rm --cached result
          '');
        };
        # nix run ".#destroy"
        destroy = {
          type = "app";
          program = toString (pkgs.writers.writeBash "destroy" ''
            if [[ -e config.tf.json ]]; then rm -f config.tf.json; fi
            ${pkgs.git}/bin/git add -Nf result \
              && cp ${terraformConfiguration} config.tf.json \
              && ${terraform}/bin/terraform init \
              && ${terraform}/bin/terraform destroy \
              && rm -f output.json \
              && ${pkgs.git}/bin/git rm --cached result
          '');
        };
        # nix run ".#clean-ssh"
        clean-ssh = {
          type = "app";
          program = toString (pkgs.writers.writeBash "clean-ssh" ''
            for ip in $(${pkgs.jq}/bin/jq -r '.[].value' output.json); do
              ssh-keygen -R "$ip"
            done
          '');
        };
        # nix run ".#deploy"
        deploy = {
          type = "app";
          program = toString (pkgs.writers.writeBash "deploy" ''
            [ -f output.json ] || apply
            ${pkgs.git}/bin/git add -Nf output.json
            ${pkgs.colmena}/bin/colmena apply
            ${pkgs.git}/bin/git rm --cached output.json
          '');
        };
        # nix run
        default = self.apps.${system}.apply;
      };

      # deploy
      colmena = let
        inherit (builtins) fromJSON readFile foldl' attrNames;
        output = fromJSON (readFile ./output.json);
      in {
        meta = { nixpkgs = pkgs; };
        defaults = import ./deploys/consul;
      } // foldl' (a: b: a // b) { } (map (name: {
        # generate hosts by output.json generated by terraform
        ${name} = {
          deployment = {
            targetHost = output.${name}.value;
            keys = import ./keys/consul.nix;
          };
        };
      }) (attrNames output));
    };
}
